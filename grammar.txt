<program> --> <otherFunctions> <mainFunction>
<mainFunction> --> TK_MAIN <stmts> TK_END
<otherFunctions> --> <function> <otherFunctions> | eps
<function> --> TK_FUNID <inputPar> <outputPar> TK_SEM <stmts> TK_END
<inputPar> --> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameterList> TK_SQR
<outputPar> --> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameterList> TK_SQR | eps
<parameterList> --> <dataType> TK_ID <remainingList>
<dataType> --> <primitiveDatatype> | <constructedDatatype>
<primitiveDatatype> --> TK_INT | TK_REAL
<constructedDatatype> --> TK_RECORD TK_RUID | TK_UNION TK_RUID | TK_RUID
<remainingList> --> TK_COMMA <parameterList> | eps
<stmts> --> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> --> <actualOrRedefined> <typeDefinitions> | eps
<actualOrRedefined> --> <typeDefinition> | <definetypeStmt>
<typeDefinition> --> TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD | TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions> --> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> --> TK_TYPE <dataType> TK_COLON TK_FIELDID TK_SEM
<moreFields> --> <fieldDefinition> <moreFields> | eps
<declarations> --> <declaration> <declarations> | eps
<declaration> --> TK_TYPE <dataType> TK_COLON TK_ID <globalOrNot> TK_SEM
<globalOrNot> --> TK_COLON TK_GLOBAL | eps
<otherStmts> --> <stmt> <otherStmts> | eps
<stmt> --> <assignmentStmt> | <iterativeStmt> | <conditionalStmt> | <ioStmt> | <funCallStmt>
<assignmentStmt> --> <singleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<singleOrRecId> --> TK_ID <optionSingleConstructed>
<optionSingleConstructed> --> eps | <oneExpansion> <moreExpansions>
<oneExpansion> --> TK_DOT TK_FIELDID
<moreExpansions> --> <oneExpansion> <moreExpansions> | eps
<funCallStmt> --> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> --> TK_SQL <idList> TK_SQR TK_ASSIGNOP | eps
<inputParameters> --> TK_SQL <idList> TK_SQR
<iterativeStmt> --> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
<conditionalStmt> --> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <elsePart> TK_ENDIF
<elsePart> --> TK_ELSE <stmt> <otherStmts> | eps
<ioStmt> --> TK_READ TK_OP <var> TK_CL TK_SEM | TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression> --> <term> <moreTerms> | TK_OP <arithmeticExpression> TK_CL | <var>
<moreTerms> --> <lowPrecedenceOperator> <term> <moreTerms> | eps
<term> --> <factor> <moreFactors>
<moreFactors> --> <highPrecedenceOperator> <factor> <moreFactors> | eps
<factor> --> TK_OP <arithmeticExpression> TK_CL | <var>
<highPrecedenceOperator> --> TK_MUL | TK_DIV
<lowPrecedenceOperator> --> TK_PLUS | TK_MINUS
<booleanExpression> --> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL | <var> <relationalOp> <var> | TK_NOT TK_OP <booleanExpression> TK_CL
<var> --> TK_NUM | TK_RNUM | <singleOrRecId>
<logicalOp> --> TK_AND | TK_OR
<relationalOp> --> TK_LT | TK_LE | TK_EQ | TK_GT | TK_GE | TK_NE
<returnStmt> --> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> --> TK_SQL <idList> TK_SQR | eps
<idList> --> TK_ID <moreIds>
<moreIds> --> TK_COMMA <idList> | eps
<definetypeStmt> --> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A> --> TK_RECORD | TK_UNION
